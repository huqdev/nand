class Main {
    function void main() {
        var Maze m;
        var Player player;
        var char key;
        var int seed;
        var boolean adminMode;
        
        // start
        do Output.printString("Press any Key to start");
        let adminMode = false;
        
        // wait for user Input and set seed
        let key = Keyboard.keyPressed();
        let seed = 42;
        while(key = 0) {
            let key = Keyboard.keyPressed();
            do Sys.wait(10);
            let seed = seed + 1;
        }
        do Random.setSeed(seed);
        
        // wait for user to release key before starting the game
        while(~(key = 0)) {
            let key = Keyboard.keyPressed();
            do Sys.wait(10);
        }
        
        // init random maze
        do Screen.clearScreen();
        let m = Maze.new(32,32);
        do m.setBorder();
        do m.loadConfig(4);
        do m.update();
        let player = m.getPlayer();
        
        // gameplay Loop
        while(true) {
        let key = Keyboard.keyPressed();
        // a to turn to west and move 1 step 
        if(key = 65) {
            do player.turnTo(3);
            do player.move(m);
            do m.update();
        }
        // d to turn east and move 1 step 
        if(key = 68) {
            do player.turnTo(1);
            do player.move(m);
            do m.update();
        }
        // e to turn the player Clockwise
        if(key = 69) {
            do player.turnClockwise();
            do m.update();
        }
        // q to turn the player counter Clockwise
        if(key = 81) {
            do player.turnCounterClockwise();
            do m.update();
        }
        
        // s to turn south and move 1 step
        if(key = 83) {
            do player.turnTo(2);
            do player.move(m);
            do m.update();
        }
        // w to turn north and move 1 step 
        if(key = 87) {
            do player.turnTo(0);
            do player.move(m);
            do m.update();
        }

        // esc to toggle adminMode
        if(key = 140) {
            let adminMode = ~adminMode;
            // when entering admin mode, ask for confirmation
            if(adminMode) {
                let adminMode = Main.askConfirmAdmin();
            }
        }
        
        // admin only commands
        // c to clear all fog 
        if(adminMode & (key = 67)) {
            do m.clearFog();
            do m.update();
        }
        // if a number is pressed, load the corresponding preset
        if(adminMode & (~(key = 0))) {
            do m.loadConfig(key - 48);
            do m.update();
        }
        
        while(~(key = 0)) {
            let key = Keyboard.keyPressed();
            // f to go fast
            if(key = 70){
                do Sys.wait(100);
                do player.move(m);
                do m.update();
            }
        }
        }
        return;
    }

    function boolean askConfirmAdmin() {
        var char key;
        let key = Keyboard.keyPressed();
        while (key = 0) {
            let key = Keyboard.keyPressed();       
        }
        // Y to confirm
        if(key = 89) {
            return true;
        }
        return false;
    }
}
