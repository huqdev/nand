class Main {
    static Maze maze;
    static MazeMenu menu;
    static Player player;
    static char key;
    static boolean adminMode;
    function void main() {
        var Status status;
        var int seed;
        // start
        do Output.moveCursor(0,0);
        do Output.printString("Press any Key to start");
        let adminMode = false;
        
        // wait for user Input and set seed
        let key = Keyboard.keyPressed();
        let seed = 42;
        while(key = 0) {
            let key = Keyboard.keyPressed();
            do Sys.wait(10);
            let seed = seed + 1;
        }
        do Random.setSeed(seed);
        
        // wait for user to release key before starting the game
        while(~(key = 0)) {
            let key = Keyboard.keyPressed();
            do Sys.wait(10);
        }
        do Screen.clearScreen();
        // init MazeMenu
        let menu = MazeMenu.new();
        do menu.setAdminMode(adminMode);
        
        // init random maze
        let maze = Maze.new(32,32);
        do maze.setBorder();
        do maze.update();
        do maze.loadConfig(4);
        do maze.update();
        let player = maze.getPlayer();
        
        // refresh menu after maze generation
        let status = maze.exportStatus();     
        do menu.setStatus(status);
        do menu.update();
        
        // gameplay Loop
        while(true) {
            let key = Keyboard.keyPressed();
            
            // after any key has been pressed update the menu
            if(~(key = 0)) {
            }
            
            // a to turn to west and move 1 step 
            if((key = 65) | (key = 97)) {
                do player.turnTo(3);
                do player.move(maze);
                do maze.update();
            }
            // d to turn east and move 1 step 
            if((key = 68) | (key = 100)) {
                do player.turnTo(1);
                do player.move(maze);
                do maze.update();
            }
            // e to turn the player Clockwise
            if((key = 69) | (key = 101)) {
                do player.turnClockwise();
                do maze.update();
            }
            // q to turn the player counter Clockwise
            if((key = 81) | (key = 113)) {
                do player.turnCounterClockwise();
                do maze.update();
            }
            
            // s to turn south and move 1 step
            if((key = 83) | (key = 115)) {
                do player.turnTo(2);
                do player.move(maze);
                do maze.update();
            }
            // w to turn north and move 1 step 
            if((key = 87) | (key = 119)) {
                do player.turnTo(0);
                do player.move(maze);
                do maze.update();
            }

            // esc to toggle adminMode
            if(key = 140) {
                let adminMode = ~adminMode;
                // when entering admin mode, ask for confirmation
                if(adminMode) {
                    let adminMode = Main.askConfirmAdmin();
                }
                do menu.setAdminMode(adminMode);
            }
            
            // admin only commands
            // c to clear all fog 
            if(adminMode & ((key = 67) | (key = 99))) {
                do maze.clearFog();
                do maze.update();
            }
            // if a number is pressed, load the corresponding preset
            if(adminMode & (~(key = 0))) {
                do maze.loadConfig(key - 48);
                do maze.update();
            }
            
            while(~(key = 0)) {
                let key = Keyboard.keyPressed();
                
                // f to go fast
                if((key = 70) | (key = 102)){
                    do player.move(maze);
                    do maze.update();
                }
                do Sys.wait(100);
            }
        }
        return;
    }

    function boolean askConfirmAdmin() {
        var char key;
        do menu.enablePopup(1);
        let key = Keyboard.keyPressed();
        while(~(key = 0)) {
            let key = Keyboard.keyPressed();       
        }
        while (key = 0) {
            let key = Keyboard.keyPressed();       
        }
        do maze.update();
        // y to confirm
        if((key = 89) | (key = 121)) {
            return true;
        }
        return false;
    }
}
